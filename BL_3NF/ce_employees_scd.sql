BEGIN;
CREATE SCHEMA IF NOT EXISTS BL_3NF;
CREATE SCHEMA IF NOT EXISTS BL_CL;

DROP TABLE IF EXISTS BL_3NF.CE_EMPLOYEES_SCD;
CREATE TABLE IF NOT EXISTS BL_3NF.CE_EMPLOYEES_SCD
(
    EMPLOYEE_ID     BIGINT,
    EMPLOYEE_SRC_ID VARCHAR(255)    NOT NULL,
    FIRST_NAME      VARCHAR(50)     NOT NULL,
    LAST_NAME       VARCHAR(50)     NOT NULL,
    EMAIL           VARCHAR(100)    NOT NULL,
    ADDRESS_ID      BIGINT          NOT NULL,
    START_DT        TIMESTAMP       NOT NULL,
    END_DT          TIMESTAMP       NOT NULL,
    IS_ACTIVE       BOOLEAN         NOT NULL,
    SOURCE_SYSTEM   VARCHAR(255)    NOT NULL,
    SOURCE_ENTITY   VARCHAR(255)    NOT NULL,
    INSERT_DT       TIMESTAMP       NOT NULL,
    UPDATE_DT       TIMESTAMP       NOT NULL,
    CONSTRAINT      PK_CE_EMPLOYEES_SCD_EMPLOYEE_ID_START_DT PRIMARY KEY (EMPLOYEE_ID, START_DT),
    CONSTRAINT      FK_CE_ADDRESSES_ADDRESS_ID FOREIGN KEY (ADDRESS_ID) REFERENCES BL_3NF.CE_ADDRESSES(ADDRESS_ID),
    CONSTRAINT      UNQ_CE_EMPLOYEES_SCD_EMPLOYEE_SRC_ID_START_DT UNIQUE (EMPLOYEE_SRC_ID, START_DT)
);

DROP SEQUENCE IF EXISTS BL_3NF.SEQ_EMPLOYEE_ID;
CREATE SEQUENCE IF NOT EXISTS BL_3NF.SEQ_EMPLOYEE_ID START WITH 1 INCREMENT BY 1;

DROP PROCEDURE IF EXISTS BL_CL.SP_3NF_LOAD_CE_EMPLOYEES_SCD_DEFAULT;
CREATE OR REPLACE PROCEDURE BL_CL.SP_3NF_LOAD_CE_EMPLOYEES_SCD_DEFAULT()
LANGUAGE PLPGSQL
AS $$
DECLARE
    V_AFFECTED BIGINT;
BEGIN
    INSERT INTO BL_3NF.CE_EMPLOYEES_SCD (EMPLOYEE_ID, EMPLOYEE_SRC_ID, FIRST_NAME, LAST_NAME, EMAIL, ADDRESS_ID, START_DT, END_DT, IS_ACTIVE, SOURCE_SYSTEM, SOURCE_ENTITY, INSERT_DT, UPDATE_DT)
    VALUES      (-1,
                'N/A',
                'N/A',
                'N/A',
                'N/A',
                -1,
                '1900-01-01 00:00:00',
                '9999-12-31 23:59:59',
                TRUE,
                'MANUAL',
                'MANUAL',
                '1900-01-01 00:00:00',
                '1900-01-01 00:00:00')
    ON CONFLICT (EMPLOYEE_ID, START_DT) DO NOTHING;
    
    GET DIAGNOSTICS V_AFFECTED = ROW_COUNT;
    CALL BL_CL.SP_MTA_INSERT_LOG('SP_3NF_LOAD_CE_EMPLOYEES_SCD_DEFAULT', 'BL_CL', 'LOAD PERFORMED SUCCESSFULLY', 1, V_AFFECTED, 0, 'N/A', 'N/A');
EXCEPTION WHEN OTHERS THEN
    CALL BL_CL.SP_MTA_INSERT_LOG('SP_3NF_LOAD_CE_EMPLOYEES_SCD_DEFAULT', 'BL_CL', 'ERROR LOADING CE_EMPLOYEES_SCD', -1, -1, -1, UPPER(SQLSTATE), UPPER(SQLERRM));
    COMMIT;
    RAISE EXCEPTION 'ERROR LOADING CE_EMPLOYEES_SCD (%): %', UPPER(SQLSTATE), UPPER(SQLERRM);
END;
$$;

DROP FUNCTION IF EXISTS BL_CL.FN_3NF_TRANSFORM_EMPLOYEES_SCD;
CREATE OR REPLACE FUNCTION BL_CL.FN_3NF_TRANSFORM_EMPLOYEES_SCD()
RETURNS TABLE
(
    EMPLOYEE_ID     VARCHAR(255),
    EVENT_DT        TIMESTAMP,
    FIRST_NAME      VARCHAR(50),
    LAST_NAME       VARCHAR(50),
    EMAIL           VARCHAR(100),
    ADDRESS_ID      BIGINT,
    SOURCE_SYSTEM   VARCHAR(255),
    SOURCE_ENTITY   VARCHAR(255)
)
LANGUAGE PLPGSQL
AS $$
DECLARE
    V_LAST_LOAD_DT TIMESTAMP;
BEGIN
    SELECT  LAST_LOAD_DT
    INTO    V_LAST_LOAD_DT
    FROM    BL_CL.MTA_LOADS
    WHERE   PROCEDURE_NAME = 'SP_3NF_LOAD_CE_EMPLOYEES_SCD' AND PROCEDURE_SCHEMA = 'BL_CL';
    
    IF V_LAST_LOAD_DT IS NULL THEN
        V_LAST_LOAD_DT := '1900-01-01 00:00:00';
    END IF;

    RETURN QUERY
        SELECT          UPPER(TRIM(SRC.EMPLOYEE_ID))::VARCHAR,
                        TRIM(SRC.TIMESTAMP)::TIMESTAMP AS EVENT_DT,
                        COALESCE(UPPER(TRIM(SRC.EMPLOYEE_FIRST_NAME)), 'N/A')::VARCHAR,
                        COALESCE(UPPER(TRIM(SRC.EMPLOYEE_LAST_NAME)), 'N/A')::VARCHAR,
                        COALESCE(UPPER(TRIM(SRC.EMPLOYEE_EMAIL)), 'N/A')::VARCHAR,
                        COALESCE(CE_ADD.ADDRESS_ID, -1),
                        'SA_OFFLINE'::VARCHAR,
                        'SRC_OFFLINE_RETAIL_SALES'::VARCHAR
        FROM            SA_OFFLINE.SRC_OFFLINE_RETAIL_SALES AS SRC
        LEFT OUTER JOIN BL_3NF.CE_COUNTRIES                 AS CE_COU ON UPPER(TRIM(SRC.PURCHASE_COUNTRY)) = CE_COU.COUNTRY_NAME
        LEFT OUTER JOIN BL_3NF.CE_CITIES                    AS CE_CIT ON UPPER(TRIM(SRC.PURCHASE_CITY)) = CE_CIT.CITY_NAME
        LEFT OUTER JOIN BL_3NF.CE_ADDRESSES                 AS CE_ADD ON UPPER(TRIM(SRC.PURCHASE_ADDRESS)) = CE_ADD.ADDRESS_NAME
        WHERE           SRC.EMPLOYEE_ID IS NOT NULL AND
                        SRC.LOAD_DT > V_LAST_LOAD_DT
        ORDER BY        EVENT_DT ASC;
END;
$$;

DROP PROCEDURE IF EXISTS BL_CL.SP_3NF_LOAD_CE_EMPLOYEES_SCD;
CREATE OR REPLACE PROCEDURE BL_CL.SP_3NF_LOAD_CE_EMPLOYEES_SCD()
LANGUAGE PLPGSQL
AS $$
DECLARE
    V_REC           RECORD;
    V_EMPLOYEE      RECORD;
    V_EMPLOYEE_NEXT RECORD;
    V_INSERTED_ROWS BIGINT := 0;
    V_UPDATED_ROWS  BIGINT := 0;
    V_TOTAL_ROWS    BIGINT := 0;
    V_AFFECTED      BIGINT := 0;
BEGIN
    FOR V_REC IN SELECT * FROM BL_CL.FN_3NF_TRANSFORM_EMPLOYEES_SCD() LOOP
        SELECT  *
        INTO    V_EMPLOYEE
        FROM    BL_3NF.CE_EMPLOYEES_SCD
        WHERE   EMPLOYEE_SRC_ID = V_REC.EMPLOYEE_ID AND
                V_REC.EVENT_DT BETWEEN START_DT AND END_DT;
        
        SELECT      *
        INTO        V_EMPLOYEE_NEXT
        FROM        BL_3NF.CE_EMPLOYEES_SCD
        WHERE       EMPLOYEE_SRC_ID = V_REC.EMPLOYEE_ID AND
                    START_DT > V_REC.EVENT_DT
        ORDER BY    START_DT ASC
        LIMIT       1;    
        
        IF V_EMPLOYEE IS NOT NULL AND (V_EMPLOYEE.FIRST_NAME != V_REC.FIRST_NAME OR V_EMPLOYEE.LAST_NAME != V_REC.LAST_NAME OR V_EMPLOYEE.EMAIL != V_REC.EMAIL OR V_EMPLOYEE.ADDRESS_ID != V_REC.ADDRESS_ID) THEN
            UPDATE  BL_3NF.CE_EMPLOYEES_SCD
            SET     END_DT = V_REC.EVENT_DT - INTERVAL '1 MINUTE',
                    IS_ACTIVE = FALSE,
                    UPDATE_DT = CURRENT_TIMESTAMP
            WHERE   EMPLOYEE_ID = V_EMPLOYEE.EMPLOYEE_ID AND
                    START_DT = V_EMPLOYEE.START_DT;
            
            GET DIAGNOSTICS V_AFFECTED = ROW_COUNT;
            V_UPDATED_ROWS := V_UPDATED_ROWS + V_AFFECTED;
            
            IF V_EMPLOYEE_NEXT IS NOT NULL AND V_EMPLOYEE_NEXT.FIRST_NAME = V_REC.FIRST_NAME AND V_EMPLOYEE_NEXT.LAST_NAME = V_REC.LAST_NAME AND V_EMPLOYEE_NEXT.EMAIL = V_REC.EMAIL AND V_EMPLOYEE_NEXT.ADDRESS_ID = V_REC.ADDRESS_ID THEN
                UPDATE  BL_3NF.CE_EMPLOYEES_SCD
                SET     START_DT = V_REC.EVENT_DT,
                        UPDATE_DT = CURRENT_TIMESTAMP
                WHERE   EMPLOYEE_ID = V_EMPLOYEE_NEXT.EMPLOYEE_ID AND
                        START_DT = V_EMPLOYEE_NEXT.START_DT;
                
                GET DIAGNOSTICS V_AFFECTED = ROW_COUNT;
                V_UPDATED_ROWS := V_UPDATED_ROWS + V_AFFECTED;
            ELSE
                INSERT INTO BL_3NF.CE_EMPLOYEES_SCD (EMPLOYEE_ID, EMPLOYEE_SRC_ID, FIRST_NAME, LAST_NAME, EMAIL, ADDRESS_ID, START_DT, END_DT, IS_ACTIVE, SOURCE_SYSTEM, SOURCE_ENTITY, INSERT_DT, UPDATE_DT)
                VALUES      (V_EMPLOYEE.EMPLOYEE_ID,
                            V_REC.EMPLOYEE_ID,
                            V_REC.FIRST_NAME,
                            V_REC.LAST_NAME,
                            V_REC.EMAIL,
                            V_REC.ADDRESS_ID,
                            V_REC.EVENT_DT,
                            CASE WHEN V_EMPLOYEE_NEXT IS NULL THEN '9999-12-31 23:59:59' ELSE V_EMPLOYEE_NEXT.START_DT - INTERVAL '1 MINUTE' END,
                            CASE WHEN V_EMPLOYEE_NEXT IS NULL THEN TRUE ELSE FALSE END,
                            V_REC.SOURCE_SYSTEM,
                            V_REC.SOURCE_ENTITY,
                            CURRENT_TIMESTAMP,
                            CURRENT_TIMESTAMP);
            
                GET DIAGNOSTICS V_AFFECTED = ROW_COUNT;
                V_INSERTED_ROWS := V_INSERTED_ROWS + V_AFFECTED;
            END IF;      
        ELSIF V_EMPLOYEE IS NULL AND V_EMPLOYEE_NEXT IS NOT NULL THEN
            IF V_EMPLOYEE_NEXT.FIRST_NAME = V_REC.FIRST_NAME AND V_EMPLOYEE_NEXT.LAST_NAME = V_REC.LAST_NAME AND V_EMPLOYEE_NEXT.EMAIL = V_REC.EMAIL AND V_EMPLOYEE_NEXT.ADDRESS_ID = V_REC.ADDRESS_ID THEN
                UPDATE  BL_3NF.CE_EMPLOYEES_SCD
                SET     START_DT = V_REC.EVENT_DT,
                        UPDATE_DT = CURRENT_TIMESTAMP
                WHERE   EMPLOYEE_ID = V_EMPLOYEE_NEXT.EMPLOYEE_ID AND
                        START_DT = V_EMPLOYEE_NEXT.START_DT;
                
                GET DIAGNOSTICS V_AFFECTED = ROW_COUNT;
                V_UPDATED_ROWS := V_UPDATED_ROWS + V_AFFECTED;
            ELSE
                INSERT INTO BL_3NF.CE_EMPLOYEES_SCD (EMPLOYEE_ID, EMPLOYEE_SRC_ID, FIRST_NAME, LAST_NAME, EMAIL, ADDRESS_ID, START_DT, END_DT, IS_ACTIVE, SOURCE_SYSTEM, SOURCE_ENTITY, INSERT_DT, UPDATE_DT)
                VALUES      (V_EMPLOYEE_NEXT.EMPLOYEE_ID,
                            V_REC.EMPLOYEE_ID,
                            V_REC.FIRST_NAME,
                            V_REC.LAST_NAME,
                            V_REC.EMAIL,
                            V_REC.ADDRESS_ID,
                            V_REC.EVENT_DT,
                            V_EMPLOYEE_NEXT.START_DT - INTERVAL '1 MINUTE',
                            FALSE,
                            V_REC.SOURCE_SYSTEM,
                            V_REC.SOURCE_ENTITY,
                            CURRENT_TIMESTAMP,
                            CURRENT_TIMESTAMP);
            
                GET DIAGNOSTICS V_AFFECTED = ROW_COUNT;
                V_INSERTED_ROWS := V_INSERTED_ROWS + V_AFFECTED;
            END IF;
        ELSIF V_EMPLOYEE IS NULL AND V_EMPLOYEE_NEXT IS NULL THEN
            INSERT INTO BL_3NF.CE_EMPLOYEES_SCD (EMPLOYEE_ID, EMPLOYEE_SRC_ID, FIRST_NAME, LAST_NAME, EMAIL, ADDRESS_ID, START_DT, END_DT, IS_ACTIVE, SOURCE_SYSTEM, SOURCE_ENTITY, INSERT_DT, UPDATE_DT)
            VALUES      (NEXTVAL('BL_3NF.SEQ_EMPLOYEE_ID'),
                        V_REC.EMPLOYEE_ID,
                        V_REC.FIRST_NAME,
                        V_REC.LAST_NAME,
                        V_REC.EMAIL,
                        V_REC.ADDRESS_ID,
                        V_REC.EVENT_DT,
                        '9999-12-31 23:59:59',
                        TRUE,
                        V_REC.SOURCE_SYSTEM,
                        V_REC.SOURCE_ENTITY,
                        CURRENT_TIMESTAMP,
                        CURRENT_TIMESTAMP);
    
            GET DIAGNOSTICS V_AFFECTED = ROW_COUNT;
            V_INSERTED_ROWS := V_INSERTED_ROWS + V_AFFECTED;
        END IF;
        V_TOTAL_ROWS := V_TOTAL_ROWS + 1;       
    END LOOP;
    
    CALL BL_CL.SP_MTA_UPDATE_LOAD('SP_3NF_LOAD_CE_EMPLOYEES_SCD', 'BL_CL');
    CALL BL_CL.SP_MTA_INSERT_LOG('SP_3NF_LOAD_CE_EMPLOYEES_SCD', 'BL_CL', 'LOAD PERFORMED SUCCESSFULLY', V_TOTAL_ROWS, V_INSERTED_ROWS, V_UPDATED_ROWS, 'N/A', 'N/A');
EXCEPTION WHEN OTHERS THEN
    CALL BL_CL.SP_MTA_INSERT_LOG('SP_3NF_LOAD_CE_EMPLOYEES_SCD', 'BL_CL', 'ERROR LOADING CE_EMPLOYEES_SCD', -1, -1, -1, UPPER(SQLSTATE), UPPER(SQLERRM));
    COMMIT;
    RAISE EXCEPTION 'ERROR LOADING CE_EMPLOYEES_SCD (%): %', UPPER(SQLSTATE), UPPER(SQLERRM);
END;
$$;
COMMIT;